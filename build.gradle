plugins {
	id 'java-library'
	id 'eclipse'
}

group = 'etri'
version = '24.01.12'

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

ext {
	logback_version = '1.3.14'
	guava_version = '33.4.8-jre'
	lz4_java_version = '1.7.1'
	commons_cli_version = '1.4'
	mvel2_version = '2.4.14.Final'
	gson_version = '2.10.1'
	commons_text_version = '1.8'
	rxjava3_version = '3.1.8'
	
	junit_version = '4.12'
	mockito_version = '3.12.4'
}

repositories {
    mavenCentral()
}

dependencies {
	implementation project(":utils")

	// Logback
	implementation "ch.qos.logback:logback-classic:${logback_version}"
	
	// Guava
	implementation "com.google.guava:guava:${guava_version}"
	
	implementation "org.lz4:lz4-java:${lz4_java_version}"
	implementation "commons-cli:commons-cli:${commons_cli_version}"
	implementation "org.mvel:mvel2:${mvel2_version}"
	implementation "org.apache.commons:commons-text:${commons_text_version}"
	implementation "com.google.code.gson:gson:${gson_version}"
	implementation "io.reactivex.rxjava3:rxjava:${rxjava3_version}"
	
	testImplementation "junit:junit:${junit_version}"
	testImplementation "org.mockito:mockito-core:${mockito_version}"
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

javadoc {
	options.encoding = 'UTF-8'
	options.links("https://docs.oracle.com/javase/8/docs/api/")
}
task javadocJar(type: Jar) {
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}

eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'build/classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						if ( entry.path.contains('/test/') ) {
							entry.output = 'build/test_classes'
						}
						else {
							entry.output = 'build/classes'
						}
					}
				}
			}
		}
	}
}